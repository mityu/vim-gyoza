" call themis#log(readfile(expand('<sfile>'))) | finish
let s:assert = themis#helper('assert')
call themis#helper('command').with(s:assert)
call themis#func_alias(s:assert)

let s:A = ScriptFuncs('autoload/gyoza/applier.vim')
let s:C = ScriptFuncs('autoload/gyoza/config.vim')

Describe Applier class
  Before all
    runtime autoload/gyoza.vim
    command ResetOptions
      \ set expandtab& smarttab& shiftwidth& tabstop& backspace&
    call test_override('char_avail', 1)
    let NewConfig = s:C.new_rule_stack
    filetype indent on
    call gyoza#applier#set_callback_on_finish_applicant({-> 0})
  End

  After all
    %bwipeout!
    ResetOptions
    call test_override('char_avail', 0)
    unlet NewConfig
    delcommand ResetOptions
    filetype indent off
    call gyoza#applier#set_callback_on_finish_applicant(v:null)
  End

  Before each
    %bwipeout!
    ResetOptions
    set filetype=vim
  End

  It is a validation.
    " Check for the auto indentation.
    Feedkeys iif foo<CR>aaa<CR>endif<ESC>
    Assert Equals(GetAllLines(), ['if foo', "\taaa", 'endif'])
  End

  It completes closing pair with no indentation.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif aaa<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), ['if aaa', "\t", 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
      \.run()
  End

  It completes closing pair with indentation.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('i<Tab>if aaa<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), ["\tif aaa", "\t\t", "\tendif"])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 3])})
      \.run()
  End

  It completes closing pair with some rules.
    let rules = NewConfig()
      \.add_rule('^\s*if\>', 'endif')
      \.add_rule('^\s*while\>', 'endwhile')
      \._rules
    call NewScripter()
      \.feedkeys('iwhile<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), ['while', "\t", 'endwhile'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
      \.run()
  End

  It applies the first matched rule.
    let rules = NewConfig()
      \.add_rule('^\s*if\>', 'endif')
      \.add_rule('^\s*if\>', 'end')
      \._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\t", 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
      \.run()
  End

  It does not complete pair if next line has deeper indentation.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call setline(1, ['if aaa', "\tfoo"])
    call NewScripter()
      \.feedkeys('ggo')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), ['if aaa', "\t", "\tfoo"])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
      \.run()
  End

  It does not complete pair if there's already the pair.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call setline(1, ['if', 'endif'])
    call NewScripter()
      \.feedkeys('ggo')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\t", 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
      \.run()
  End

  It handles indentation properly when using hardtabs in space-indented buffer.
    set tabstop=2 shiftwidth=2 expandtab
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call setline(1, ["\tif"])
    call NewScripter()
      \.feedkeys('o')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), ["\tif", '    ', '  endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 5])})
      \.run()
  End

  It handles indentation properly when using softtabs in hardtab-indented buffer.
    set tabstop=2 shiftwidth=2
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call setline(1, ['  if'])
    call NewScripter()
      \.feedkeys('o')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines())})
  End

  It handles indentation properly when using softtabs in hardtab-indented buffer.
    set tabstop=4 shiftwidth=2
  End

  It cancel pair completion properly when using hardtabs in space-indented buffer.
    set tabstop=2 shiftwidth=2
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call setline(1, ['  if', '  endif'])
    call NewScripter()
      \.feedkeys('ggo')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), ['  if', "\t\t", '  endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 3])})
      \.run()
  End

  It handles pair completion properly when using softtabs in hardtab-indented buffer.
    set tabstop=2 shiftwidth=2 expandtab
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    let lines = map(['if', 'endif'], '"\t" . v:val')
    call setline(1, lines)
    call NewScripter()
      \.feedkeys('ggo')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), insert(copy(lines), '    ', 1))})
      \.call({-> s:assert.equals(GetCurpos(), [2, 5])})
      \.run()
  End

  It doesn't complete pair when second newline.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('')
      \.call({-> s:assert.equals(GetAllLines(), ['if', '', "\t", 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [3, 2])})
      \.run()
  End

  It removes current line if leaving insert mode just after newline.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('<ESC>')
      \.call({-> s:assert.equals(GetAllLines(), ['if', 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 1])})
      \.run()
  End

  It removes current line if leaving insert mode just after newline (with indentation).
    set expandtab smarttab shiftwidth=2
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('i<Tab>if<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('<ESC>')
      \.call({-> s:assert.equals(GetAllLines(), ['  if', '  endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 3])})
      \.run()
  End

  It doesn't remove current line when leaving insert mode after typing some words.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('foo<ESC>')
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\tfoo", 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 4])})
      \.run()
  End

  It doesn't remove current line when leaving insert mode after typing <C-h>.
    set backspace=indent
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
      \.feedkeys('<C-u>')
      \.call({-> s:assert.equals(GetAllLines(), ['if', '', 'endif'])})
      \.feedkeys('<ESC>')
      \.call({-> s:assert.equals(GetAllLines(), ['if', '', 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 1])})
      \.run()
  End

  It doesn't remove current line when type <CR> twice.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('<ESC>')
      \.call({-> s:assert.equals(GetAllLines(), ['if', '', '', 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [3, 1])})
      \.run()
  End

  It skips rule applies when some text exists before cursor.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> [setline('.', "\treturn "), cursor(2, 9)]})
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\treturn "])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 9])})
      \.run()
  End

  It doesn't apply rule when canceler is on the next line (literal).
    let lines =<< trim END
    if
    else
    endif
    END

    let rules = NewConfig().add_rule('^\s*if\>', 'endif', ['else'])._rules

    call setline(1, lines)
    call NewScripter()
      \.feedkeys('ggo')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), insert(copy(lines), "\t", 1))})
      \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
      \.feedkeys('<CR>')
      \.call({-> s:assert.equals(GetAllLines(), flatten(insert(copy(lines), ['', "\t"], 1)))})
      \.call({-> s:assert.equals(GetCurpos(), [3, 2])})
      \.feedkeys('<ESC>')
      \.run()
  End

  It doesn't apply rule when canceler is on the next line (regexp).
    let lines =<< trim END
    if
    elseif condition
    endif
    END
    call map(lines, '"\t" . v:val')  " Add an indentation.

    let rules = NewConfig().add_rule('^\s*if\>', 'endif', ['\=^elseif\>\s'])._rules

    call setline(1, lines)
    call NewScripter()
      \.feedkeys('ggo')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), insert(copy(lines), "\t\t", 1))})
      \.call({-> s:assert.equals(GetCurpos(), [2, 3])})
      \.feedkeys('<ESC>')
      \.run()
  End

  It doesn't consider as valid canceler if indentation depth is different (literal).
    let lines =<< trim END
    if foo
      else
    END

    let rules = NewConfig().add_rule('^\s*if\>', 'endif', ['else'])._rules

    set expandtab smarttab shiftwidth=2
    call setline(1, lines)
    call NewScripter()
      \.feedkeys('ggo')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), insert(copy(lines), "  ", 1))})
      \.call({-> s:assert.equals(GetCurpos(), [2, 3])})
      \.run()
  End

  It doesn't consider as valid canceler if indentation depth is different (regexp).
    let lines =<< trim END
    if cond1
      elseif cond2
    END

    let rules = NewConfig().add_rule('^\s*if\>', 'endif', ['\=^elseif\>\s'])._rules

    set expandtab smarttab shiftwidth=2
    call setline(1, lines)
    call NewScripter()
      \.feedkeys('ggo')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), insert(copy(lines), "  ", 1))})
      \.call({-> s:assert.equals(GetCurpos(), [2, 3])})
      \.run()
  End

  It can undo step by step.
    set expandtab smarttab shiftwidth=2

    let undo0 =<< trim END
    if
      aaaa
    endif
    END

    let undo1 =<< trim END
    if
    endif
    END

    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('aaaa<ESC>')
      \.run()
    Assert Equals(GetAllLines(), undo0)
    Assert Equals(GetCurpos(), [2, 6])
    normal! u
    Assert Equals(GetAllLines(), undo1)
    Assert Equals(GetCurpos(), [2, 5])
    normal! u
    Assert Equals(GetAllLines(), [''])
    Assert Equals(GetCurpos(), [1, 1])
  End

  It doesn't create a undo block for newline removal.
    set expandtab smarttab shiftwidth=2

    let undo0 =<< trim END
    if
    endif
    END

    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('<ESC>')
      \.run()
    Assert Equals(GetAllLines(), undo0)
    Assert Equals(GetCurpos(), [2, 1])
    normal! u
    Assert Equals(GetAllLines(), [''])
    Assert Equals(GetCurpos(), [1, 1])
  End

  It doesn't create a new undo block after newline if user created it.
    set expandtab smarttab shiftwidth=2

    let undo0 =<< trim END
    if
      a
    endif
    END

    let undo1 =<< trim END
    if

    endif
    END
    let undo1[1] = '  '

    let undo2 = ['']

    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules

    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), undo1)})
      \.feedkeys('<C-g>ua')
      \.call({-> s:assert.equals(GetAllLines(), undo0)})
      \.feedkeys('<ESC>')
      \.run()
    Assert Equals(GetAllLines(), undo0)
    Assert Equals(GetCurpos(), [2, 3])
    normal! u
    Assert Equals(GetAllLines(), undo1)
    Assert Equals(GetCurpos(), [2, 2])
    normal! u
    Assert Equals(GetAllLines(), undo2)
    Assert Equals(GetCurpos(), [1, 1])
  End

  It keeps text after cursor when the first rule is applied.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call setline(1, 'iffoo')
    call NewScripter()
      \.feedkeys('^la<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\tfoo", 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
      \.run()
  End

  It keeps text after cursor when the second rule is applied.
    let rules = NewConfig()
      \.add_rule('^\s*while\>', 'endwhile')
      \.add_rule('^\s*if\>', 'endif')
      \._rules
    call setline(1, 'iffoo')
    call NewScripter()
      \.feedkeys('^la<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\tfoo", 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
      \.run()
  End

  It forgets kept text on next application when rule is defined with a function.
    let rules = NewConfig().add_rule('^\s*if', {lines ->
      \ {'pair': 'endif', 'cursor_text': lines.current}})._rules
    call setline(1, 'iffoo')
    call NewScripter()
      \.feedkeys('^la<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\tfoo", 'endif'])})
      \.feedkeys('<ESC>Goif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\tfoo", 'endif', 'if', "\t", 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [5, 2])})
      \.run()
  End

  It completes pair when block is nested.
    let rules = NewConfig()
      \.add_rule('^\s*if\>', 'endif')
      \.add_rule('^\s*for\>', 'endfor')
      \._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('for<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\tfor", "\t\t", "\tendfor", 'endif'])})
      \.call({-> s:assert.equals(GetCurpos(), [3, 3])})
      \.feedkeys('if<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetCurpos(), [4, 4])})
      \.call({-> s:assert.equals(getline(3, 5), ["\t\tif", "\t\t\t", "\t\tendif"])})
      \.run()
  End

  It works well even when cursor text has "|".
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call setline(1, 'ifx|xxx')
    call cursor(1, 1)
    call NewScripter()
      \.feedkeys('la<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\tx|xxx", 'endif'])})
      \.run()
  End

  It does not collapse cursor text when it contains special character like "<Tab>".
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call setline(1, 'if<Tab>')
    call cursor(1, 1)
    call NewScripter()
      \.feedkeys('la<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.call({-> s:assert.equals(GetAllLines(), ['if', "\t<Tab>", 'endif'])})
      \.run()
  End

  It clears temporal mappings properly.
    let rules = NewConfig().add_rule('^\s*if\>', 'endif')._rules
    call NewScripter()
      \.feedkeys('iif<CR>')
      \.call({-> gyoza#applier#trigger_applicant(rules)})
      \.feedkeys('<ESC>')
      \.run()
    let maps = execute('imap <Plug>')->trim()->split("\n")
    Assert Equals(maps, ['No mapping found'])
  End

  Context when {pair} is a function
    It calls {pair} to get pair text.
      let rules = NewConfig().add_rule('^\s*if\>', {_ -> {'pair': 'endif'}})._rules
      call NewScripter()
        \.feedkeys('iif<CR>')
        \.call({-> gyoza#applier#trigger_applicant(rules)})
        \.call({-> s:assert.equals(GetAllLines(), ['if', "\t", 'endif'])})
        \.run()
    End

    It skips a rule application.
      " The first rule should be skipped.
      let rules = NewConfig()
        \.add_rule('^\s*while\>', {_ -> {'skip': 'this', 'pair': 'end'}})
        \.add_rule('^\s*while\>', {_ -> {'pair': 'endwhile'}})
        \._rules
      call NewScripter()
        \.feedkeys('iwhile<CR>')
        \.call({-> gyoza#applier#trigger_applicant(rules)})
        \.call({-> s:assert.equals(GetAllLines(), ['while', "\t", 'endwhile'])})
        \.run()
    End

    It skips the all rules application.
      " All the rules should be skipped.
      let rules = NewConfig()
        \.add_rule('^\s*while\>', {_ -> {'skip': 'all', 'pair': 'end'}})
        \.add_rule('^\s*while\>', {_ -> {'pair': 'endwhile'}})
        \._rules
      call NewScripter()
        \.feedkeys('iwhile<CR>')
        \.call({-> gyoza#applier#trigger_applicant(rules)})
        \.call({-> s:assert.equals(GetAllLines(), ['while', "\t"])})
        \.run()
    End

    It can change the cursor line text.
      let rules = NewConfig()
        \.add_rule('^\s*if\>', {_ -> {'pair': 'endif', 'cursor_text': 'expr'}})
        \._rules
      call NewScripter()
        \.feedkeys('iif<CR>')
        \.call({-> gyoza#applier#trigger_applicant(rules)})
        \.call({-> s:assert.equals(GetAllLines(), ['if', "\texpr", 'endif'])})
        \.run()
    End

    It can add literal cancelers.
      let rules = NewConfig()
        \.add_rule('^\s*if\>', {_ -> {'pair': 'endif', 'cancelers': ['else']}})
        \._rules
      call setline(1, ['if', 'else'])
      call NewScripter()
        \.feedkeys('ggo')
        \.call({-> gyoza#applier#trigger_applicant(rules)})
        \.call({-> s:assert.equals(GetAllLines(), ['if', "\t", 'else'])})
        \.run()
    End

    It can add regexp cancelers.
      let rules = NewConfig()
        \.add_rule('^\s*if\>', {_ -> {'pair': 'endif', 'cancelers': ['\=^\s*elseif\>']}})
        \._rules
      call setline(1, ['if', 'elseif foo'])
      call NewScripter()
        \.feedkeys('ggo')
        \.call({-> gyoza#applier#trigger_applicant(rules)})
        \.call({-> s:assert.equals(GetAllLines(), ['if', "\t", 'elseif foo'])})
        \.run()
    End

    It calls {pair} with buffer state unchanged.
      let call_count = []
      let l:PairFunc = {retval, line -> [
        \ add(call_count, 1),
        \ s:assert.equals(GetAllLines(), ['if', "\tfoo"]),
        \ s:assert.equals(GetCurpos(), [2, 2]),
        \ retval,
        \][-1]}
      let rules = NewConfig()
        \.add_rule('^\s*if\>', funcref(l:PairFunc, [{'skip': 'this'}]))
        \.add_rule('^\s*if\>', funcref(l:PairFunc, [{'skip': 'this'}]))
        \.add_rule('^\s*if\>', funcref(l:PairFunc, [{'pair': 'endif'}]))
        \._rules
      call setline(1, 'iffoo')
      call NewScripter()
        \.feedkeys('^la<CR>')
        \.call({-> gyoza#applier#trigger_applicant(rules)})
        \.call({-> s:assert.equals(GetAllLines(), ['if', "\tfoo", 'endif'])})
        \.call({-> s:assert.equals(GetCurpos(), [2, 2])})
        \.run()
      Assert LengthOf(call_count, 3)
    End

    It does not collapse cursor text when it contains special character like "<Tab>".
      const F = {lines -> [
        \   s:assert.equals(lines.current, '<Space>'),
        \   {'pair': 'endif', 'cursor_text': '<Tab>'},
        \ ][1]}
      let rules = NewConfig()
        \.add_rule('^\s*if\>', 'endif', ['guard'])
        \.add_rule('^\s*if\>', F)
        \._rules
      call setline(1, ['if<Space>', 'guard'])
      call cursor(1, 1)
      call NewScripter()
        \.feedkeys('la<CR>')
        \.call({-> gyoza#applier#trigger_applicant(rules)})
        \.call({-> s:assert.equals(GetAllLines(), ['if', "\t<Tab>", 'endif', 'guard'])})
        \.run()
    End

  End

End
